plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'androidx.navigation.safeargs.kotlin'
    id 'dagger.hilt.android.plugin'
}

def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(rootProject.file("keystore.properties")))

android {
    sourceSets {
        def debugReleaseSrcDir = 'src/debugRelease/java'
        def localDebugSrcDir = 'src/localDebug/java'
        local {
            java.srcDirs = [localDebugSrcDir]
        }
        debug {
            java.srcDirs = [localDebugSrcDir, debugReleaseSrcDir]
        }
        release {
            java.srcDirs = [debugReleaseSrcDir]
        }
    }

    signingConfigs {
        release {
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }
    compileSdk 31

    defaultConfig {
        applicationId "io.github.cbinarycastle.betty"
        minSdk 24
        targetSdk 31
        versionCode 4
        versionName "1.1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField "String", "BACKEND_URL", '"http://macao-alb-796421278.ap-northeast-2.elb.amazonaws.com"'
            buildConfigField "String", "ADMOB_BANNER_AD_UNIT_ID", '"ca-app-pub-1258768914276560/5408579081"'
            buildConfigField "String", "ADMOB_NATIVE_AD_UNIT_ID", '"ca-app-pub-1258768914276560/1964554870"'
            buildConfigField "String", "AMPLITUDE_API_KEY", '"5a919f0ba1939e63407d6bf970a3200f"'
            buildConfigField "String", "HACKLE_SDK_KEY", '"oeTjS34w9B2gohu2C3y2tJqZz3rKcj6n"'
            signingConfig signingConfigs.release
        }
        debug {
            buildConfigField "String", "BACKEND_URL", '"http://3.34.28.237:10001"'
            buildConfigField "String", "ADMOB_BANNER_AD_UNIT_ID", '"ca-app-pub-3940256099942544/6300978111"'
            buildConfigField "String", "ADMOB_NATIVE_AD_UNIT_ID", '"ca-app-pub-3940256099942544/2247696110"'
            buildConfigField "String", "AMPLITUDE_API_KEY", '"dbe7bbf79508b33bd56ef8f526179bc4"'
            buildConfigField "String", "HACKLE_SDK_KEY", '"kLSiTrOcSypVj9LvBMA4m47RxugIe5iq"'
        }
        local {
            initWith debug
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion = "$composeVersion"
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
    hilt {
        enableTransformForLocalTests = true
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.0'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.2'

    // Fragment
    debugImplementation "androidx.fragment:fragment-testing:$fragmentVersion"

    // Lifecycle
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycleVersion"

    // Coroutine
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutineVersion"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutineVersion"

    // Compose
    implementation "androidx.compose.runtime:runtime:$composeVersion"
    implementation "androidx.compose.ui:ui:$composeVersion"
    implementation "androidx.compose.ui:ui-tooling:$composeVersion"
    implementation "androidx.compose.material:material:$composeVersion"
    implementation "androidx.compose.material:material-icons-extended:$composeVersion"

    // Navigation
    implementation "androidx.navigation:navigation-compose:$navComposeVersion"
    implementation "androidx.navigation:navigation-ui-ktx:$navVersion"
    testImplementation "androidx.navigation:navigation-testing:$navVersion"

    // Paging
    implementation "androidx.paging:paging-runtime-ktx:$pagingVersion"
    implementation "androidx.paging:paging-compose:$pagingComposeVersion"

    // Hilt
    implementation "com.google.dagger:hilt-android:$hiltVersion"
    implementation "androidx.hilt:hilt-navigation-compose:$hiltComposeVersion"
    kapt "com.google.dagger:hilt-android-compiler:$hiltVersion"
    testImplementation "com.google.dagger:hilt-android-testing:$hiltVersion"
    kaptTest "com.google.dagger:hilt-android-compiler:$hiltVersion"

    // OkHttp
    implementation "com.squareup.okhttp3:logging-interceptor:$okHttpVersion"

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"

    // Accompanist
    implementation "com.google.accompanist:accompanist-placeholder:$accompanistVersion"
    implementation "com.google.accompanist:accompanist-swiperefresh:$accompanistVersion"

    // Landscapist
    implementation "com.github.skydoves:landscapist-glide:$landscapistVersion"

    // Collapsing toolbar
    implementation "me.onebone:toolbar-compose:$collapsingToolbarVersion"

    // Timber
    implementation "com.jakewharton.timber:timber:$timberVersion"

    // ThreeTen
    implementation "com.jakewharton.threetenabp:threetenabp:$threeTenVersion"

    // AdMob
    implementation "com.google.android.gms:play-services-ads:$adMobVersion"

    // Amplitude
    implementation "com.amplitude:android-sdk:$amplitudeVersion"

    // Hackle
    implementation "io.hackle:hackle-android-sdk:$hackleVersion"
    implementation "io.hackle:hackle-sdk-core:$hackleVersion"

    // JUnit
    testImplementation "junit:junit:$jUnitVersion"
    testImplementation "androidx.test.ext:junit:$androidJUnitVersion"
    androidTestImplementation "androidx.test.ext:junit:$androidJUnitVersion"

    // Espresso
    testImplementation "androidx.test.espresso:espresso-core:$espressoVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espressoVersion"

    // Robolectric
    testImplementation "org.robolectric:robolectric:$robolectricVersion"

    // Native ad template
    implementation project(':nativetemplates')
}